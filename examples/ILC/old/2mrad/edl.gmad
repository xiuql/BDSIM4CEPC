! **************************************************************
!
!  2 mrad ILC extraction line - interaction region
!  gmad file by Agapov, J Carter after Y. Nosochkov et. al.
!  update: 11 May 2006
!
! **************************************************************


! NB: the initial coordinate system is a mediane 
! between the incoming and outgoing beamlines 
! right after start the coordinates are rotated by 1 mrad abd then the
! ir goes on axis. Initial beam should have 1mrad angle offset



! **************************************************************



! beam nominal energy [gev];

 ener  := 250;	
 gamma := ener /  0.510999e-3;

! brho [kg m];

 brho := ener / .0299792458;

! horizontal angle of extracted beam with respect to incoming beam at ip.;

 xang := 2.0e-3;



! *******************  ip beam parameters (twiss)*************************

! undisrupted;

 bx0 := 30.0e-3;
 by0 :=  3.0e-4;
 ax0 :=  0;
 ay0 :=  0;
 ex0 :=  1.0e-5 / gamma;
 ey0 :=  4.0e-8 / gamma;
 dp0 :=  0.1e-2;

! disrupted;

 bxd :=  9.673e-3;
 byd :=  3.401e-4;
 axd :=  1.840;
 ayd :=  0.6153;
 exd :=  3.125e-5 / gamma;
 eyd :=  8.859e-8 / gamma;
 dpd :=  0.080;

! 1st and 2nd order dispersion at exit of sf1 for +2 mrad angle;

 dxsf1  := -31.1940530e-3;
 dpxsf1 :=  -4.2010044e-3;

 ddxsf1  := 45.1140e-3;
 ddpxsf1 :=  8.7734e-3;


! ***********************  beamline *****************************

! shared magnets on incoming line;

 mip : marker;

 rq1 := 0.6;
 rq3 := 0.2;

 rq4 := 0.1*m;

 ld0 := 4.5;
 d0  : drift, l= ld0;

 lqd0 :=  2.5;
 !kqd0 := -0.095803008082;
 !kqd0 :=-0.096027184064;
 kqd0 :=-0.09527184064;

 rqd0 :=  35e-3;
 qd0  : quadrupole, l=lqd0/2, k1= kqd0, aper= rqd0;

 ld1a0 := 1.195;
 d1a0  : drift, l= ld1a0;

 lsd0a := 3.8;
 !ksd0  := 0.625408483406;
 ksd0 := 0.613667397116;
 rsd0a := 88e-3;
 sd0a  : sextupole, l= lsd0a/2, k2= ksd0, aper= rsd0a;

 ld1b := 3.8694;
 d1b  : drift, l= ld1b;

! the main qf1 gradient is not seen by extracted beam.;
! the field in the qf1 pocket on the extraction path will be defined;
! by multipoles.;

 lqf1 := 2.0;
 !kqf1 := 0.04030118243;
 kqf1 := 0.040898286679;
 rqf1 := 10e-3    ;! main aperture for incoming beam;
 rqf1 := 115e-3   ;! pocket aperture for extracted beam;
! qf1  : quadrupole, l= lqf1/2, k1= kqf1, aper= rqf1;
 qf1  : drift, l= lqf1/2, aper= rqf1;

! approximate scaling of multipole field seen by extracted beam in the qf1 pocket;
! assuming r=10 mm qf1 pole tip radius.;

! set scmqf1 (kqf1 / 0.096307727548);
! value scmqf1;

scmqf1 := (kqf1 / 0.096307727548);

! turn off the qf1 main gradient (it is not seen by extr. beam);

! set kqf1 (0.0);

! multipoles for qf1 field in the pocket on extraction path.;
! cherrill's 8 cm model of qf1.;
! the qf1 field in the pocket is expanded with respect to qf1 magnet;
! x-center, therefore these multipoles must be horizontally offset;
! with respect to extraction s-axis.;
! this field is only valid for x-range from 15 to 80 mm from qf1 center.;
! the factors 1/4 are for lqf1 equal to full length of qf1.;

 k0mqf1 :=   0.001400676408 * scmqf1;
 k1mqf1 :=  -0.051858065964 * scmqf1;
 k2mqf1 :=   1.564934143676 * scmqf1;
 k3mqf1 := -24.657655185322 * scmqf1;

 lmqf1 := 0.001;
 !lmqf1 := 0.2;

 m0qf1 : hkicker, l=lmqf1, angle = -k0mqf1*lqf1/4, aper=rq3; ! the sign is for mad;
 m1qf1 : quadrupole, l= lmqf1, k1= k1mqf1*lqf1/lmqf1/4., aper=rq3;
 m2qf1 : sextupole, l= lmqf1, k2= k2mqf1*lqf1/lmqf1/4., aper=rq3;
 m3qf1 : octupole, l= lmqf1, k3= k3mqf1*lqf1/lmqf1/4., aper=rq3;


 ld1c := 0.5;
 d1c  : drift, l= ld1c, aper=rq3;

! extracted beam passes through large aperture of sf1;

 lsf1 := 3.8;
 !ksf1 := -0.203936131169;
 ksf1 := -0.202686916843;
 rsf1 := 112e-3;
 sf1  : sextupole, l= lsf1/2, k2= ksf1, aper= rsf1;

! drift to first extraction quadrupole;


 ldtoexa := 1*mm; ! gap between the sextupole and actual beamline 
                  !rotation point
 ldtoex := 11.9;
 dtoexa : drift, l= ldtoexa, aper=200*mm;
 dtoexb : drift, l= ldtoex - ldtoexa, aper=200*mm;

! extraction quadrupoles;

 lqex1 := 3.0;
 lqex2 := 0.0;

 rqex1a := 113e-3;
 rqex1b := 127e-3;
 rqex2  := 127e-3;

 kqex1a := 0.704991753181e-2;
 kqex1b := 0.62727612684608661417e-2   ;! =kqex1a * (rqex1a/rqex1b);
 kqex2  := 0.0;

 qex1a : quadrupole, l= lqex1/2, k1= kqex1a, aper= rqex1a;
 qex1b : quadrupole, l= lqex1/2, k1= kqex1b, aper= rqex1b;


 ! extraction quadrupoles with photon aperture

 qex1am : element, l= lqex1, geometry="mokka:sql/qex1am.sql";
 qex1bm : element, l= lqex1, geometry="mokka:sql/qex1bm.sql";
 ecoladqexm : element, l=0.6, geometry="mokka:sql/ecoladqexm.sql";

! qex2  : quadrupole, l= lqex2/2, k1= kqex2 , aper= rqex2;

 lqex3 := 3.0;
 lqex4 := 3.0;
 lqex5 := 3.0;

 rqex3 := rq3;
 rqex4 := rq3;
 rqex5 := rq3;

 kqex3 :=  0.409812455491e-2;
 kqex4 := -0.444774102105e-2;
 kqex5 :=  kqex3 *1.0;

 qex3  : quadrupole, l= lqex3/2, k1= kqex3, aper= rqex3;
 qex4  : quadrupole, l= lqex4/2, k1= kqex4, aper= rqex4;
 qex5  : quadrupole, l= lqex5/2, k1= kqex5, aper= rqex5;

 lqex6 := 3.0;
 lqex7 := 3.0;

 rqex6 := rq3;
 rqex7 := rq3;

 kqex6 :=  0.345819663541e-2;
 kqex7 := -0.33529690787e-2;

 qex6  : quadrupole, l= lqex6/2, k1= kqex6, aper= rqex6;
 qex7  : quadrupole, l= lqex7/2, k1= kqex7, aper= rqex7;

! extraction sextupoleupoles turned off;

 lsex1 := 2.0;
 ksex1 := 0.0;
 rsex1 := 135e-3;
 sex1  : sextupole, l= lsex1/2, k2= ksex1, aper= rsex1;

 sex1m  : element, l= lsex1, geometry="mokka:sql/sex1m.sql"; ;

 lsex2 := 2.0;
 ksex2 := 0.0;
 rsex2 := rq3;
 sex2  : sextupole, l= lsex2/2, k2= ksex2, aper= rsex2;

 lsex3 := 2.0;
 ksex3 := 0.0;
 rsex3 := rq3;
 sex3  : sextupole, l= lsex3/2, k2= ksex3, aper= rsex3;

 lsex4 := 2.0;
 ksex4 := 0.0;
 rsex4 := rq3;
 sex4  : sextupole, l= lsex4/2, k2= ksex4, aper= rsex4;

! horizontal deflecting bends;

 lbhex1 := 2.0;
 abhex1 := 0.5e-3;

 bhex1  : sbend, l= lbhex1, angle= abhex1, aper=rq3;
 	  	 ! e1= abhex1/2, e2= abhex1/2,
                 !hgap= rq3, fint= 0.5, fintx= 0.5;

 lbhex2 := 2.0;
 abhex2 := -0.717237106393e-3;
 bhex2  : sbend, l= lbhex2, angle= abhex2, aper=rq3;
 	  	 ! e1= abhex2/2, e2= abhex2/2,
                 !hgap= rq3, fint= 0.5, fintx= 0.5;

! abhex3 is constrained by 2 mrad angle at 2nd ip;

 lbhex3 := 2.0;
 abhex3 := (-2.0e-3 +5.52325458164e-3 -abhex1 -2*abhex2) / 4;

 bhex3  : sbend, l= lbhex3, angle= abhex3, aper=rq3;
 	  	 ! e1= abhex3/2, e2= abhex3/2,
                 !hgap= rq3, fint= 0.5, fintx= 0.5;

 lbhex4 := 2.0;
 abhex4 := -1.04016361717e-3;

 bhex4  : sbend, l= lbhex4, angle= abhex4, aper=rq3;
 	  	 ! e1= abhex4/2, e2= abhex4/2,
                 !hgap= rq3, fint= 0.5, fintx= 0.5;

 lbhex5 := 2.0;
 abhex5 := 0.967012919787e-3;

 bhex5  : sbend, l= lbhex5, angle= abhex5, aper=rq3;
 	  	 ! e1= abhex5/2, e2= abhex5/2,
                 !hgap= rq3, fint= 0.5, fintx= 0.5;

! collimation vertical chicane bends;

 vrot : transform3d, psi=pi/2;
 ivrot : transform3d, psi=-pi/2;


 lychic := 2.0;
 aychic := 1.0e-3;

 bychic:  sbend, l=lychic, angle= aychic, aper=10*cm, outR=15*cm;
 	  	 ! e1= aychic/2, e2= aychic/2, tilt,
                 !hgap= rq3, fint= 0.5, fintx= 0.5;
 bychicm: sbend, l=lychic, angle=-aychic, aper=10*cm, outR=15*cm;
 	  	 ! e1=-aychic/2, e2=-aychic/2, tilt,
                 !hgap= rq3, fint= 0.5, fintx= 0.5;

 ldtocol := 15.0;
 dtocol  : drift, l= ldtocol, aper=rq3;
 !dtocolm : element, l=ldtocol, geometry="mokka:sql/dtcol.sql";


 dy2chic : drift, l= 4.0, aper=10*cm;
 dychic  : drift, l= 16.4, aper=10*cm;
!dychic2 : drift, l= dychic[l]-hcol2[l]-dbex[l];
 dychic2 : drift, l= 15.8, aper=rq3;

! vertical energy chicane bends;

 lyene := 2.0;
 ayene := 1.0e-3;

 byene:  sbend, l=lyene, angle= ayene, aper=rq3;
 	 	! e1= ayene/2, e2= ayene/2, tilt,
                !hgap= rq3, fint= 0.5, fintx= 0.5;
 byenem: sbend, l=lyene, angle=-ayene, aper=rq3;
 	 	! e1=-ayene/2, e2=-ayene/2, tilt,
                !hgap= rq3, fint= 0.5, fintx= 0.5;

 dyene1 : drift, l= 1.0;
 dyene2 : drift, l= 16.4;
 dyene3 : drift, l= 9.4;

! vertical polarimeter chicane bends (sbend + 2 drifts);

 lypol := 2.0;
 aypol := 1.0e-3;

 bypol :  sbend, l= lypol, angle= aypol, aper=rq3;
       	  	 ! e1= aypol/2, e2= aypol/2, tilt,
                 !hgap= rq3, fint= 0.5, fintx= 0.5;
 bypolm : sbend, l= lypol, angle=-aypol, aper=rq3;       
 	  	 ! e1=-aypol/2, e2=-aypol/2, tilt,
                 !hgap= rq3, fint= 0.5, fintx= 0.5;

 dypol1 : drift, l= 1.0;
 dypol2 : drift, l= 5.7;
 dypol3 : drift, l= 2.0;
 dfoc2  : drift, l= 4.0;
 dfoc2b : drift, l= 4.0;

! collimators (the larger non-collimating gap is arbitrarily set to 1 m);

 vcolgap  :=  12e-3;
 hcolgap  :=  75e-3;
 vcolgap2 :=  50e-3;
 hcolgap2 :=  60e-3;
 hcolgap3 :=  75e-3;
 ecolgapa := 110e-3;
 ecolgap0 :=  75e-3;
 ecolgap1 := 120e-3;
 ecolgap2 := 130e-3;
 ecolgap3 := 140e-3;

 vcol  : rcol, l=0.3, xsize= 1.0, ysize= vcolgap,material="Tungsten" ;
 hcol  : rcol, l=0.3, xsize= hcolgap, ysize= 1.0, material="Tungsten";

 hcolm : element , l =0.3, geometry="mokka:sql/hcol.sql"; 

 vcol2 : rcol, l=0.3, xsize= 1.0, ysize= vcolgap2, material="Tungsten";
 hcol2 : rcol, l=0.3, xsize= hcolgap2, ysize= 1.0, material="Tungsten";
 hcol3 : rcol, l=0.3, xsize= hcolgap3, ysize= 1.0, material="Tungsten";
 ecola : ecol, l=0.3, xsize= ecolgapa, ysize= ecolgapa, material="Tungsten";
 ecol0 : ecol, l=0.3, xsize= ecolgap0, ysize= ecolgap0, material="Tungsten";
 ecol1 : ecol, l=0.3, xsize= ecolgap1, ysize= ecolgap1, material="Tungsten" ;
 ecol2 : ecol, l=0.3, xsize= ecolgap2, ysize= ecolgap2, material="Tungsten";
 ecol3 : ecol, l=0.3, xsize= ecolgap3, ysize= ecolgap3, material="Tungsten";

! other drifts;

 dbex   : drift, l= 0.3, aper=rq3;
 dbex0  : drift, l= 0.3, aper=10*cm;
 dbex1  : drift, l= 20.0, aper=rq3;

 dqex   : drift, l= 0.3, aper=200*mm;
 dqex1  : drift, l= 2.5;
 dqex2  : drift, l= 2.5-hcol3[l]-dbex[l] ;
 dqex3  : drift, l= 2.0;

 dqbex  : drift, l= 1.0,aper=220*mm;
 dqbex1 : drift, l= 2.0;
 dqbex2 : drift, l= 1.0;
 dqbex3 : drift, l= 1.0;

 dtodump : drift, l= 35.0;

 mexfoc1 : marker;
 mexfoc2 : marker;
 mexdump : marker;
 mexdx0  : marker;
 mtoex   : marker;
 mexwx   : marker;
 mexwy   : marker;
	

dumpelement: element,l=2*m, geometry="mokka:sql/dump.sql";


! Transform at end of SF1. Values are mean of bunch distribution at that point.
!tdex1 : transform3d, x=0.0807926192977*m,
!                     phi=0.0052325458164; 

tdex1 : transform3d, x=0.0812033*m,
                     phi=0.00565803; 



mark1 : marker;
mark2 : marker;
mark3 : marker;
mark4 : marker;
mark5 : marker;
mark6 : marker;
mark7 : marker;
mark8 : marker;
mark9 : marker;
mark10: marker;

! beamlines;

! beamline from ip to end of sf1 with shared incoming and extraction;
! magnets.;

dtemp : drift, l=1e-6*m;

d0xh: drift, l=4.5*m;
qd0h : quadrupole, l=2.5*m, k1=kqd0;
d1a0h : drift, l= 1.195*m;
sd0h : sextupole, l=3.8*m,k2=ksd0;


ir: line=(d0xh,qd0h,d1a0h,sd0h);

!ir: element, l=11.995*m,geometry="mokka:sql/geomlist.sql", 
!                        bmap="mokka:sql/SiD_fields.txt";

!ir: element, l=11.995*m, geometry="mokka:sql/geomlist.sql";

!ir : drift, l=11.995*m;

qf1tot : line = ( m0qf1,  m1qf1,  m2qf1,  m3qf1,  
                 qf1, 
                 m0qf1,  m1qf1,  m2qf1,  m3qf1, 
                 m0qf1,  m1qf1,  m2qf1,  m3qf1, 
                 qf1, 
		 m0qf1,  m1qf1,  m2qf1,  m3qf1);



k0qf1 =-k0mqf1;
k1qf1= k1mqf1;
k2qf1= k2mqf1;
k3qf1= k3mqf1;


qf1totm : multipole, l=lqf1, knl={k0qf1,k1qf1,k2qf1,k3qf1}, 
	                     ksl={0,0,0,0}, aper=rq3;

ipsf1 : line=(tdip, ir,d1b, qf1tot, d1c,sf1, sf1,mexdx0,mark2);

! beamline after sf1 - can be used for dispersion calculation with;
! initial dxsf1, dpxsf1.;



sf1sw : line=( dtoexa,
               tdex1,
               dtoexb, 
               ecola,   dqex,  mtoex, 
               2*qex1a,  dqex, 
               2*qex1b,  dqex, 
               2*sex1,   dqex, 
               !2*qex2,   
	       dqbex, 
               bhex1);

sf1swm : line=(  mark1,
                 dtoexa,
                 tdex1, 
                 dtoexb, mark2, 
                 ecoladqexm, mark3, 
                 qex1am,  
                 dqex, mark4,
                 qex1bm,  
                 dqex, mark5,
                 sex1m,   mark6,dqex, mark7, 
                 !2*qex2,   
		 dqbex, mark8,
                 bhex1);

chicb1 : line=(vrot, bychic,  ivrot,   dbex);

! gamma beamline to dump

lpdump := 1*m;

pdump: element, l=lpdump, geometry="mokka:sql/pdump.sql";

ldtopa := 10*cm;
ldtopb := 30*m;
ldtopc := 230*m;
ldtop := ldtopa + ldtopb + ldtopc;

swxofs := -0.156*m;
swphi := -0.00273;

dtopda: drift, l=ldtopa, aper=4*cm;
dtopdb: drift, l=ldtopb, aper=6*cm;
dtopdc: drift, l=ldtopc, aper=16*cm;

tr2 : transform3d, x=swxofs, phi=swphi;

pdumpl : line=(tr2,dtopda, mark10, dtopdb, dtopdc, pdump);


! e- beamline to dump
swtodump:line=(  dy2chic, 
		 vrot, bychic,  ivrot,   dbex0, 
		 vrot, bychic,  ivrot,   dbex0, 
		 vrot, bychic,  ivrot,   dychic, 
		 vrot, bychicm, ivrot,   dbex0, 
		 vrot, bychicm, ivrot,   dbex0, 
		 vrot, bychicm, ivrot, 
                 vcol2,   mexfoc1, vrot, 
                 bychicm,  ivrot,  dbex, 
		 vrot, bychicm,  ivrot,  dbex, 
		 vrot, bychicm,  ivrot,  dychic2, 
                 hcol2,   dbex, vrot, 
                 bychic,  ivrot,   dbex,              !120m
		 vrot, bychic,  ivrot,   dbex, 
		 vrot, bychic,  ivrot,   dqbex1, 
                 2*qex3,   dqex, 
                 2*qex3,   dqex, 
                 2*sex2,   dqex1, 
                 2*qex4,   dqex, 
                 2*qex4,   dqex, 
                 2*qex4,   dqex, 
                 2*qex4,   dqex2, 
                 hcol3,   dbex, 
                 2*sex3,   dqex, 
                 2*qex5,   dqex, 
                 2*qex5,   dqbex2, 
                 bhex2,    dbex, 
                 bhex2,    dyene1, 
		 vrot, 
                 byene,  ivrot,    dbex, 
		 vrot, 
                 byene,  ivrot,    dbex, 
		 vrot, 
                 byene,  ivrot,    dyene2, 
		 vrot, 
                 byenem,  ivrot,   dbex, 
		 vrot, 
                 byenem,  ivrot,   dbex, 
		 vrot, 
                 byenem,  ivrot,   dbex, 
		 vrot, 
                 byenem,  ivrot,   dbex, 
		 vrot, 
                 byenem,  ivrot,   dbex, 
		 vrot, 
                 byenem,  ivrot,   dyene2, 
		 vrot, 
                 byene,  ivrot,    dbex, 
		 vrot, 
                 byene,  ivrot,    dbex, 
		 vrot, 
                 byene,  ivrot,    dbex, 
                 2*sex4,   dyene3, 
                 ecol0,   dbex, 
                 bhex3,    dbex, 
                 bhex3,    dbex, 
                 bhex3,    dbex, 
                 bhex3,    dypol1, 
		 vrot, 
                 bypol,  ivrot,    dbex, 
		 vrot, 
                 bypol,  ivrot,    dypol2, 
		 vrot, 
                 bypolm,  ivrot,   dbex, 
		 vrot, 
                 bypolm,  ivrot,   dfoc2,  mexfoc2,  dfoc2b, 
		 vrot, 
                 bypolm,  ivrot,   dbex, 
		 vrot, 
                 bypolm,  ivrot,   dypol2, 
		 vrot, 
                 bypol,  ivrot,    dbex, 
		 vrot, 
                 bypol,  ivrot,    dypol3, 
                 bhex4,    dbex, 
                 bhex4,    dbex, 
                 bhex4,    dbex, 
                 bhex4,    dbex, 
                 bhex4,    dbex, 
                 bhex4,    dbex, 
                 bhex4,    2*dbex1, 
                 ecol1,   dbex, 
                 bhex5,    dbex, 
                 bhex5,    dbex, 
                 bhex5,    dbex, 
                 bhex5,    dqbex3, 
                 2*qex6,   dqex, 
                 2*qex6,   dqex3, 
                 2*qex7,   dqex, 
                 2*qex7,   3*dtodump, 
                 ecol2,   5*dtodump, 
                 ecol3,   1*dtodump, 
                 mexwx,  mexwy,  mexdump );


edumpl : line=(swtodump,dumpelement);

tr4a : transform3d , z=-(ldtop+lpdump)*cos(swphi);
tr4b : transform3d , phi=-swphi;


tr4c : transform3d , x=-swxofs-0.64*m;

tr4 : line=(tr4a, tr4b, tr4c);

! ************************  beamline ****************************

endl: marker;

! exline : line=(ipsf1, sf1sw, dtocol, swtodump,endl);
! exline : line=(ipsf1);
! exline : line=(dumpelement);



! e beamline
!exline : line=(ir,d1b,qf1tot, d1c,sf1, sf1,sf1sw,dtocol,mark9,hcolm,vcol,
!                swtodump,endl);


! gamma beamline
!exline : line=(ir,d1b,qf1totm, d1c,sf1, sf1,sf1swm,dtocol,mark9,hcolm,vcol,
!                pdumpl);



! both lines
exline : line=(ir,d1b,qf1totm, d1c,sf1, sf1,sf1swm,dtocol,mark9,hcolm,vcol,
               pdumpl,tr4,edumpl);


!exline : line=(ir,d1b,qf1totm, d1c,sf1, sf1,endl);

startl : marker;

!exline : line=(startl,ir,d1b,qf1totm, d1c,sf1, sf1,endl,endl);

!ddd: drift, l=0.00001*m;

!exline : line=(startl,2*qex1b,endl);
!exline : line=(startl,qex1bm,endl);

use, period=exline;

sample, range=startl;
sample, range=qf1totm;
sample, range=endl;
sample, range=d1c;
sample, range=d1b;
sample, range=sf1[1];
sample, range=sf1[2];

sample, range=d0xh;
sample, range=qd0h;
sample, range=d1a0h;
sample, range=sd0h;

!sample, range=dtocol;
!sample, range=mark1;
!sample, range=mark2;
!sample, range=mark3;
!sample, range=mark4;
!sample, range=mark5;
!sample, range=mark6;
!sample, range=mark7;
!sample, range=mark8;
!sample, range=mark9;
!sample, range=mark10;

!sample, range=mexfoc1;
sample, range=hcol2;
sample, range=ecol0;

!sample, range=endl;




!print,line;




! ********************  beam parameters ************************

      
beam,  particle="e-",
       energy=ener * GeV,
       nparticles=1e+3,
       distrType="pt[1]:E[GeV]:xp[rad]:yp[rad]:z[mum]:x[nm]:y[nm]",
       distrFile="bunches/beam.dat";



! ****************  bdsim run options parameters ***************

option, beampipeRadius = 10 * cm,
        boxSize = 1.0 * m,
        tunnelRadius= 1.0 * m,
        beampipeThickness = 0.1 * cm,
        thresholdCutCharged = 1 * MeV,
        thresholdCutPhotons = 0.01 * MeV;

option, prodCutPhotonsP=0.511*MeV;
option, prodCutElectronsP=0.511*MeV;
option, prodCutPositronsP=0.511*MeV;

option, ngenerate=200, physicsList="em", synchRadOn=0, srTrackPhotons=0, nperfile=10000;
option, stopTracks=1;

!option, storeNeutronTrajectory=1;

option, storeTrajectory=1;


! **************************  EOF  *****************************


